# Contributor: Peter Simons <simons@cryp.to>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=2.6
pkgrel=1
pkgdesc="High-level Specification Language."
arch=('i686' 'x86_64')
url='http://maude.cs.uiuc.edu/'
license=('GPL')
depends=('buddy' 'libtecla' 'gmp' 'libsigsegv')
makedepends=('flex' 'bison')
options=(!makeflags)
source=( "http://maude.cs.uiuc.edu/download/current/Maude-$pkgver.tar.gz"
         "http://maude.cs.uiuc.edu/download/current/FM2.6/full-maude26.maude"
	 "profile.sh" )
md5sums=('7b1dd753e9994f034a49588e2541b968'
         '7bab068a28777b1281455247f97af751'
         '0a51738365579574b40a3d32da6f3291')
noextract=()

build() {
  cd "$srcdir/Maude-$pkgver"
  ./configure --prefix=/usr --datadir=/usr/share/maude
  make all 
  make check 
}
package() {
  cd "$srcdir/Maude-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude26.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"
  install -D -m 555 "$srcdir/profile.sh" \
    "$pkgdir/etc/profile.d/maude.sh"
}
